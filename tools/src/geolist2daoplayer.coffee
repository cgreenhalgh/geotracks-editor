# convert geolist data from geotracks-editor to daoplayer composition.
module.exports = (geolist) ->
	# daoplayer composition skeleton
	comp = 
		meta:
			mimetype: 'application/x-daoplayer-composition'
			version: 1
			# title, description, artist
			description: 'Generated by geolist2daoplayer, '+(new Date())
		tracks: []
		scenes: []
		context:
			waypoints: []
			routes: []
			# title, description, requiredAccuracy
		# defaultScene, constants, merge
	
	# geolist should be array of geolist_entry
	# initially each entry is one scene
	for entry,ei in geolist
		scene = 
			name: 'scene_'+ei
			partial: false
			tracks: []
			# constants, vars, onload, onupdate, title, description, artist
			updatePeriod: 1
			waypoints: {}
			routes: {}
		comp.scenes.push scene
		if not entry.track?
			console.error 'Warning: entry ['+ei+'] has no track'
			continue
		if not entry.track.md5?
			console.error 'Warning: entry ['+ei+'] track has no md5'
			continue		
		scene.tracks.push
			name: entry.track.md5
			volume: 1
			# pos
		
	# each entry has a track, each unique track is one track
	tracks = {}
	for entry in geolist
		if entry.track? and entry.track.md5? and not tracks[entry.track.md5]?
			track = 
				name: entry.track.md5
				pauseIfSilent: true
				# title, description
				files: [
					path: entry.track.md5+'.'+entry.track.file_ext
					trackPos: 0
					filePos: 0
					length: entry.track.duration_s
					repeats: 1
				]
			tracks[track.name] = track
			comp.tracks.push track
	
	return comp
